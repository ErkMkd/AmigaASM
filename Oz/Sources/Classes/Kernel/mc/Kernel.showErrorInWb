;¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡
;	Method...:	showErrorInWb				27/2/1999
;	IN.......: d0=error code, a0=@object source error.
;	OUT......: -
;	ERRORS...: -
;¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡¡
Kernel.showErrorInWb
		movem.l	d0-a6,-(sp)
		bsr.w	Kernel.getErrorMessage
		move.l	d0,a1			;d0=@errorMessage
		strsize	a1,d1			;d1=stringSize
		;¡¡¡Open intuition:
		movem.l	d0/d1/a0,-(sp)
		move.l	4.w,a6
		lea	.intui(pc),a1
		clr.l	d0
		jsrLib	openLibrary
		move.l	d0,a6
		;¡¡¡Put WorkBench screen to front:
		jsrLib	WBenchToFront
		;¡¡¡Close intuition:
		move.l	a6,a1
		move.l	4.w,a6
		jsrLib	closeLibrary
		;¡¡¡Open dos.library
		lea	.dos(pc),a1
		clr.l	d0
		jsrlib	openLibrary
		move.l	d0,a6
		;¡¡¡Open window
		lea	.win(pc),a0
		move.l	a0,d1
		move.l	#1005,d2
		jsrlib	open
		tst.l	d0
		beq.w	.fin1
		lea	.whdl(pc),a0
		move.l	d0,(a0)
		;¡¡¡Write "!!! ERROR !!!"
		move.l	d0,d1
		lea	.error(pc),a0
		move.l	a0,d2
		moveq	#.obj-.error,d3
		jsrLib	write
		;¡¡¡Write "Object..."
		move.l	.whdl(pc),d1
		lea	.obj(pc),a0
		move.l	a0,d2
		moveq	#14,d3
		jsrlib	write
		;¡¡¡Write object name
		move.l	8(sp),a0
		lea	classeDef.Kernel(pc),a1
		cmp.l	object_classe(a0),a1
		beq.s	.0
		move.l	object_node+nd_name(a0),d2
		beq.s	.0
		move.l	.whdl(pc),d1
		move.l	d2,a0
		strsize	a0,d3
		jsrlib	write
		;¡¡¡Write "Classe..."
.0		move.l	.whdl(pc),d1
		lea	.clas(pc),a0
		move.l	a0,d2
		moveq	#14,d3
		jsrlib	write
		;¡¡¡Write classe name
		move.l	.whdl(pc),d1
		move.l	8(sp),a0
		lea	classeDef.Kernel(pc),a1
		cmp.l	object_classe(a0),a1
		beq.s	.classe
		move.l	object_classe(a0),a0
.classe		move.l	nd_name(a0),d2
		beq.s	.1
		move.l	d2,a0
		strsize	a0,d3
		jsrlib	write
		;¡¡¡Write "Error..."
.1		move.l	.whdl(pc),d1
		lea	.err(pc),a0
		move.l	a0,d2
		moveq	#14,d3
		jsrlib	write
		;¡¡¡Write error message
		move.l	.whdl(pc),d1
		move.l	(sp),d2
		move.l	4(sp),d3
		jsrlib	write
		;¡¡¡Wait user handshake
		;move.l	.whdl(pc),d1
		;lea	.win(pc),a0
		;move.l	a0,d2
		;moveq	#1,d3
		;jsrlib	read
.wtUser		btst	#6,$bfe001
		beq.s	.userOk
		cmp.b	#117,ciaaBase+ciaSdr
		bne.s	.wtUser
.userOk		;¡¡¡Close window
		move.l	.whdl(pc),d1
		jsrlib	close
		;¡¡¡Close dos.library
.fin1		move.l	a6,a1
		move.l	4.w,a6
		jsrlib	closeLibrary
		;¡¡¡¡Zee End:
		movem.l	(sp)+,d0/d1/a0
		movem.l	(sp)+,d0-a6
		clc
		rts

	;åååååååååååå DATAS ååååååååååååå

.whdl	dc.l	0
.win	dc.b	"CON:50/50/350/100/ Intense - Oz OS 1997-99 -",0
.dos	dc.b	"dos.library",0
.intui	dc.b	"intuition.library",0
.error	dc.b	10,"  !!! Gnaark ERROR !!! 8p -(sprrt)"
.obj	dc.b	10,10," Object...: "
.clas	dc.b	10,10," Classe...: "
.err	dc.b	10,10," Error....: "
	even
